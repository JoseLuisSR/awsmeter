plugins {
    id 'java'
}

group 'org.apache.jmeter.protocol.aws'
version '2.0.0'

repositories {
    mavenCentral()
}

class ApacheJMeterRule implements ComponentMetadataRule{
    void execute(ComponentMetadataContext context) {
        context.details.allVariants{
            withDependencies{
                removeAll {it.group in ["org.apache.jmeter","bom"] }
            }
        }
    }
}

dependencies {
    components{
        withModule("org.apache.jmeter:ApacheJMeter_java", ApacheJMeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_core", ApacheJMeterRule)
    }
    implementation 'org.apache.jmeter:ApacheJMeter_java:5.4'
    compileOnly 'org.apache.jmeter:ApacheJMeter_core:5.4'
    implementation platform('software.amazon.awssdk:bom:2.15.0')
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.971')
    implementation 'software.amazon.awssdk:kinesis'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'com.amazonaws:aws-java-sdk-sns'
    implementation 'com.amazonaws:aws-java-sdk-sts'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

task uberJar(type: Jar) {

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}