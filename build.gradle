plugins {
    id 'java'
}

group 'org.apache.jmeter.protocol.aws'
version '3.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility= JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

class ApacheJMeterRule implements ComponentMetadataRule{
    void execute(ComponentMetadataContext context) {
        context.details.allVariants{
            withDependencies{
                removeAll {it.group in ["org.apache.jmeter","bom"] }
            }
        }
    }
}

dependencies {
    components{
        withModule("org.apache.jmeter:ApacheJMeter_java", ApacheJMeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_core", ApacheJMeterRule)
    }
    implementation 'org.apache.jmeter:ApacheJMeter_java:5.6.2'
    compileOnly 'org.apache.jmeter:ApacheJMeter_core:5.6.2'
    implementation platform('software.amazon.awssdk:bom:2.21.43')
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.610')
    implementation 'software.amazon.awssdk:kinesis'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:cognitoidentityprovider'
    implementation 'software.amazon.awssdk:eventbridge'
    implementation 'com.amazonaws:aws-java-sdk-sns'
    implementation 'com.amazonaws:aws-java-sdk-sts'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

task uberJar(type: Jar) {

    duplicatesStrategy = 'include'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}